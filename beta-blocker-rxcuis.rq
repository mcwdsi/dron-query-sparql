prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

#Very close to matching output file, only with 621 entries instead of the target 612. This is likely due to absence of anti-hypertensive function
select distinct (?b as ?DrugName) (?a as ?IRI) (?c as ?RxCui)
where { 
  {?k rdfs:label ?aa}
  UNION
  {?a rdfs:label ?aa}
  FILTER (!regex(?aa, "Ophthalmic","i")) . 
  FILTER (!regex(?aa, "Suspension","i")) .
  FILTER (!regex(?aa, "Cartridge","i")) .
  ?a rdfs:label ?b . 
  ?a <http://purl.obolibrary.org/obo/DRON_00010000> ?c . 
  ?a rdfs:subClassOf* ?k .
  ?k rdfs:subClassOf ?y .
  ?k <http://purl.obolibrary.org/obo/DRON_00010000> ?e .
  ?k rdfs:label ?j .
  #?y ?w ?z .
  ?y rdf:type owl:Restriction .
  ?y owl:onProperty <http://www.obofoundry.org/ro/ro.owl#has_proper_part> .
  ?y owl:someValuesFrom ?x .
  #?x ?t ?u .
  ?x rdf:type owl:Class .
  ?x owl:intersectionOf ?s .
  # scattered molecular aggregate
  ?s rdf:first <http://purl.obolibrary.org/obo/OBI_0000576> .
  ?s rdf:rest ?q .
  ?q rdf:first ?p .
  # and is bearer of active ingredient role
  ?p rdf:type owl:Restriction .
  ?p owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000053> .
  ?p owl:someValuesFrom <http://purl.obolibrary.org/obo/DRON_00000028> .
  ?q rdf:rest ?o .
  ?o rdf:first ?n .
  ?n rdf:type owl:Restriction .
  # and has granular part some thing
  ?n owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000071> .
  ?n owl:someValuesFrom ?m .
  #?m rdfs:label 'furosemide'^^xsd:string .
  ?m rdfs:subClassOf ?g .
  ?g rdf:type owl:Restriction .
  ?g owl:onProperty ?h .
  ?g owl:someValuesFrom ?i . 
  ?i rdfs:label 'non-activating competitive beta-adrenergic receptor binding disposition'^^xsd:string
  }

