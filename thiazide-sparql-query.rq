#'drug product' and ('is bearer of' some 'anti-hypertensive function') and (has_proper_part some ('has granular part' some ('is bearer of' some 'thiazide / thiazide like')))

select (?b as ?label) (?a as ?iri) (?c as ?rxcui)

where {
  
    # has disposition some anti-hypertensive function
    ?a rdfs:label ?b . 
  	?a <http://purl.obolibrary.org/obo/DRON_00010000> ?c . 
  	?a rdfs:subClassOf* ?k .
  	?k rdfs:subClassOf ?y .
  	?k <http://purl.obolibrary.org/obo/DRON_00010000> ?e .
  	?k rdfs:label ?j .
   	?k rdfs:subClassOf ?dispositionOf .
  	?dispositionOf rdf:type owl:Restriction .
  	?dispositionOf owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000053> .
  	?dispositionOf owl:someValuesFrom <http://purl.obolibrary.org/obo/DRON_00000034> .
    # has proper part scattered molecular aggregate
    #?iri rdfs:subClassOf+ ?y .
    ?y rdf:type owl:Restriction .
    ?y owl:onProperty <http://www.obofoundry.org/ro/ro.owl#has_proper_part> .
    ?y owl:someValuesFrom ?x .
    ?x rdf:type owl:Class .
    ?x owl:intersectionOf ?s .
    ?s rdf:first <http://purl.obolibrary.org/obo/OBI_0000576> .
    ?s rdf:rest ?q .
    ?q rdf:first ?p .
    # bearer of active ingredient role
    ?p rdf:type owl:Restriction .
    ?p owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000053> .
    ?p owl:someValuesFrom <http://purl.obolibrary.org/obo/DRON_00000028> .
    ?q rdf:rest ?o .
    ?o rdf:first ?n .
    ?n rdf:type owl:Restriction .
    ?n owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000071> .
    ?n owl:someValuesFrom ?m .
    # bearer of function-inhibiting sodium-chloride con transporter binding disposition
    ?m rdf:type owl:Class .
    ?m rdfs:subClassOf+ ?h .
    ?h rdf:type owl:Restriction .
    ?h owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000053> . 
    ?h owl:someValuesFrom <http://purl.obolibrary.org/obo/DRON_00000059> .
  
  }